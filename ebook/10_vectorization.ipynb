{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Vectorization\n",
    "\n",
    "Vectorization is a style of computer programming where operations are applied to multiple elements at the same time instead of individual elements.\n",
    "\n",
    "Vector code has special registers and special instructions.\n",
    "\n",
    "Most registers are between 8 and 64 bits\n",
    "\n",
    "Vector registers are between 128 and 512 bits currently.\n",
    "\n",
    "The vector instructions treat these bits as representing a vector of multiple elements of data and perform the operation element wise.\n",
    "\n",
    "For example, take a 128 bit register.\n",
    "\n",
    "We can think of these 128 bits as representing four 32-bit elements.\n",
    "\n",
    "We can then compare multiple registers each containing these four elements and get the results of all four comparisons at once\n",
    "\n",
    "![vector_operation](images/vector_op.JPG \"vector operation\")\n",
    "\n",
    "The instructions I am describing here is [pcmpeqd](https://www.intel.com/content/www/us/en/docs/intrinsics-guide/index.html#text=_mm_cmpeq_epi32&ig_expand=878)\n",
    "\n",
    "Formally, given `a` and `b` each of which is a 128 bit register, output dest, a new 128 bit register calculated in the following way\n",
    "\n",
    "```\n",
    "FOR j := 0 to 3\n",
    "\ti := j*32\n",
    "\tdst[i+31:i] := ( a[i+31:i] == b[i+31:i] ) ? 0xFFFFFFFF : 0\n",
    "ENDFOR\n",
    "```\n",
    "\n",
    "If you want to operate on 256 bit registers you need to use a different instructions, such as [vpcmpeqd](https://www.intel.com/content/www/us/en/docs/intrinsics-guide/index.html#text=_mm256_cmpeq_epi32&ig_expand=878,879)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now let's look at a problem.\n",
    "\n",
    "Given an array of data, we want to count how many ints are equal to the target.\n",
    "\n",
    "Here is some simple code to count.\n",
    "```c\n",
    "long count_ints(int* data, long n, int target) {\n",
    "    long count = 0;\n",
    "    for (long i = 0; i < n; i++) {\n",
    "        if (data[i] == target) {\n",
    "            count+=1;\n",
    "        }\n",
    "    }\n",
    "    return count;\n",
    "}\n",
    "```\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "To help us look at code and how it compiles we are going [Compiler Explorer](https://godbolt.org/)\n",
    "\n",
    "This website allows you to, among other things, compile code online and view the assemble.\n",
    "\n",
    "Lets start by looking at the assembly for this specific problem.\n",
    "\n",
    "We can see it [https://godbolt.org/z/sdn6nx9Ph](https://godbolt.org/z/sdn6nx9Ph)\n",
    "\n",
    "We can see that the compiler is able to do some vectorization for us"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```assembly\n",
    "# -O3 -mno-sse -fno-unroll-loops \n",
    "count_ints(int*, long, int):                     # @count_ints(int*, long, int)\n",
    "        test    rsi, rsi\n",
    "        jle     .LBB0_1\n",
    "        xor     ecx, ecx\n",
    "        xor     eax, eax\n",
    ".LBB0_4:                                # %for.body\n",
    "        xor     r8d, r8d\n",
    "        cmp     dword ptr [rdi + 4*rcx], edx\n",
    "        sete    r8b\n",
    "        add     rax, r8\n",
    "        inc     rcx\n",
    "        cmp     rsi, rcx\n",
    "        jne     .LBB0_4\n",
    "        ret\n",
    ".LBB0_1:\n",
    "        xor     eax, eax\n",
    "        ret\n",
    "```\n",
    "\n",
    "\n",
    "```assembly\n",
    "# -O3 -mno-sse\n",
    "count_ints(int*, long, int):                     # @count_ints(int*, long, int)\n",
    "        test    rsi, rsi\n",
    "        jle     .LBB0_1\n",
    "        mov     ecx, esi\n",
    "        and     ecx, 3\n",
    "        cmp     rsi, 4\n",
    "        jae     .LBB0_8\n",
    "        xor     r8d, r8d\n",
    "        xor     eax, eax\n",
    "        jmp     .LBB0_4\n",
    ".LBB0_1:\n",
    "        xor     eax, eax\n",
    "        ret\n",
    ".LBB0_8:                                # %for.body.preheader.new\n",
    "        and     rsi, -4\n",
    "        xor     r8d, r8d\n",
    "        xor     eax, eax\n",
    ".LBB0_9:                                # %for.body\n",
    "        xor     r9d, r9d\n",
    "        cmp     dword ptr [rdi + 4*r8], edx\n",
    "        sete    r9b\n",
    "        add     r9, rax\n",
    "        xor     eax, eax\n",
    "        cmp     dword ptr [rdi + 4*r8 + 4], edx\n",
    "        sete    al\n",
    "        xor     r10d, r10d\n",
    "        cmp     dword ptr [rdi + 4*r8 + 8], edx\n",
    "        sete    r10b\n",
    "        add     r10, rax\n",
    "        add     r10, r9\n",
    "        xor     eax, eax\n",
    "        cmp     dword ptr [rdi + 4*r8 + 12], edx\n",
    "        sete    al\n",
    "        add     rax, r10\n",
    "        add     r8, 4\n",
    "        cmp     rsi, r8\n",
    "        jne     .LBB0_9\n",
    ".LBB0_4:                                # %for.cond.cleanup.loopexit.unr-lcssa\n",
    "        test    rcx, rcx\n",
    "        je      .LBB0_7\n",
    "        lea     rsi, [rdi + 4*r8]\n",
    "        xor     edi, edi\n",
    ".LBB0_6:                                # %for.body.epil\n",
    "        xor     r8d, r8d\n",
    "        cmp     dword ptr [rsi + 4*rdi], edx\n",
    "        sete    r8b\n",
    "        add     rax, r8\n",
    "        inc     rdi\n",
    "        cmp     rcx, rdi\n",
    "        jne     .LBB0_6\n",
    ".LBB0_7:                                # %for.cond.cleanup\n",
    "        ret\n",
    "\n",
    "```\n",
    "\n",
    "```assembly\n",
    "#-O3 -mavx2  -fno-unroll-loops \n",
    ".LCPI0_0:\n",
    "        .quad   1                               # 0x1\n",
    "count_ints(int*, long, int):                     # @count_ints(int*, long, int)\n",
    "        test    rsi, rsi\n",
    "        jle     .LBB0_1\n",
    "        cmp     rsi, 4\n",
    "        jae     .LBB0_5\n",
    "        xor     ecx, ecx\n",
    "        xor     eax, eax\n",
    "        jmp     .LBB0_8\n",
    ".LBB0_1:\n",
    "        xor     eax, eax\n",
    "        ret\n",
    ".LBB0_5:                                # %vector.ph\n",
    "        mov     rcx, rsi\n",
    "        and     rcx, -4\n",
    "        vmovd   xmm0, edx\n",
    "        vpbroadcastd    xmm1, xmm0\n",
    "        vpxor   xmm0, xmm0, xmm0\n",
    "        xor     eax, eax\n",
    "        vpbroadcastq    ymm2, qword ptr [rip + .LCPI0_0] # ymm2 = [1,1,1,1]\n",
    ".LBB0_6:                                # %vector.body\n",
    "        vpcmpeqd        xmm3, xmm1, xmmword ptr [rdi + 4*rax]\n",
    "        vpmovzxdq       ymm3, xmm3              # ymm3 = xmm3[0],zero,xmm3[1],zero,xmm3[2],zero,xmm3[3],zero\n",
    "        vpand   ymm3, ymm3, ymm2\n",
    "        vpaddq  ymm0, ymm0, ymm3\n",
    "        add     rax, 4\n",
    "        cmp     rcx, rax\n",
    "        jne     .LBB0_6\n",
    "        vextracti128    xmm1, ymm0, 1\n",
    "        vpaddq  xmm0, xmm0, xmm1\n",
    "        vpshufd xmm1, xmm0, 238                 # xmm1 = xmm0[2,3,2,3]\n",
    "        vpaddq  xmm0, xmm0, xmm1\n",
    "        vmovq   rax, xmm0\n",
    "        cmp     rcx, rsi\n",
    "        je      .LBB0_2\n",
    ".LBB0_8:                                # %for.body\n",
    "        xor     r8d, r8d\n",
    "        cmp     dword ptr [rdi + 4*rcx], edx\n",
    "        sete    r8b\n",
    "        add     rax, r8\n",
    "        inc     rcx\n",
    "        cmp     rsi, rcx\n",
    "        jne     .LBB0_8\n",
    ".LBB0_2:                                # %for.cond.cleanup\n",
    "        vzeroupper\n",
    "        ret\n",
    "\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "If you want to vectorization manually you can use what are known as [intrinsics](https://www.intel.com/content/www/us/en/docs/intrinsics-guide/index.html) \n",
    "\n",
    "These are special functions in c and c++ which compile down to a single assembly instruction \n",
    "\n",
    "For example to use [vpcmpeqd](https://www.intel.com/content/www/us/en/docs/intrinsics-guide/index.html#text=_mm256_cmpeq_epi32&ig_expand=878,879) and compare 8 32 bit integers at once we can use \n",
    "\n",
    "`__m256i _mm256_cmpeq_epi32 (__m256i a, __m256i b)`\n",
    "\n",
    "Now let's think about how we might want to approach this function.\n",
    "\n",
    "We are going to want to compare 8 elements at a time.  This means our main loop with increment by 8 each time.  This means we will also have to perform work at the end to deal with any extra iterations.  Here is a skeleton\n",
    "\n",
    "```c\n",
    "long count_ints(int* data, long n, int target) {\n",
    "  long count = 0;\n",
    "  // find the end of the main loop which is divisible by 8\n",
    "  long clean_end = (n / 8) * 8;\n",
    "  // move the i outside the loop so we can use it for the cleanup loop\n",
    "  long i = 0;\n",
    "  for (; i < clean_end; i += 8) {\n",
    "    // compare the 8 elements at positions data[i] through data[i+7]\n",
    "    int number_equal = ???;\n",
    "    count += number_equal\n",
    "  }\n",
    "  // clean up and perform the last few iterations \n",
    "  for (; i < n; i++) {\n",
    "    if (data[i] == target) {\n",
    "      count += 1;\n",
    "    }\n",
    "  }\n",
    "  return count;\n",
    "}\n",
    "```\n",
    "\n",
    "Now it is just a matter of calculating the actual comparison.\n",
    "\n",
    "The function we were looking at before will form the bases \n",
    "\n",
    "```\n",
    "__m256i _mm256_cmpeq_epi32 (__m256i a, __m256i b)\n",
    "\n",
    "FOR j := 0 to 7\n",
    "\ti := j*32\n",
    "\tdst[i+31:i] := ( a[i+31:i] == b[i+31:i] ) ? 0xFFFFFFFF : 0\n",
    "ENDFOR\n",
    "dst[MAX:256] := 0\n",
    "```\n",
    "\n",
    "So now we also need to get our comparisons into two vector registers.\n",
    "\n",
    "The first will just loading the data from the array\n",
    "\n",
    "The instruction we want is \n",
    "```\n",
    "__m256i _mm256_loadu_si256 (__m256i const * mem_addr)\n",
    "dst[255:0] := MEM[mem_addr+255:mem_addr]\n",
    "dst[MAX:256] := 0\n",
    "```\n",
    "\n",
    "The next we want is is the target int, we already have it in one int, we just need it in all locations in a vector register\n",
    "\n",
    "```\n",
    "__m256i _mm256_set1_epi32 (int a)\n",
    "\n",
    "FOR j := 0 to 7\n",
    "\ti := j*32\n",
    "\tdst[i+31:i] := a[31:0]\n",
    "ENDFOR\n",
    "dst[MAX:256] := 0\n",
    "```\n",
    "\n",
    "The last step is how to get the result back out of a vector register and into a normal int.\n",
    "\n",
    "For this we are going to use \n",
    "```\n",
    "int _mm256_movemask_epi8 (__m256i a)\n",
    "\n",
    "FOR j := 0 to 31\n",
    "\ti := j*8\n",
    "\tdst[j] := a[i+7]\n",
    "ENDFOR\n",
    "```\n",
    "\n",
    "Note that this is for each byte and not for each int, this is because there simply isn't an instruction for `_mm256_movemask_epi32`.\n",
    "\n",
    "This gives us a bit mask, we will then need to count the bits in the mask\n",
    "\n",
    "```\n",
    "int _popcnt32 (int a)\n",
    "\n",
    "dst := 0\n",
    "FOR i := 0 to 31\n",
    "\tIF a[i]\n",
    "\t\tdst := dst + 1\n",
    "\tFI\n",
    "ENDFOR\n",
    "```\n",
    "\n",
    "This will tell us how many bits are set in an integer.\n",
    "\n",
    "So if we put it all together we can fill in the function as follows \n",
    "\n",
    "\n",
    "```c\n",
    "\n",
    "long count_ints(int* data, long n, int target) {\n",
    "  long count = 0;\n",
    "  long clean_end = (n / 8) * 8;\n",
    "  long i = 0;\n",
    "  for (; i < clean_end; i += 8) {\n",
    "      auto cmp_vec = _mm256_cmpeq_epi32(\n",
    "        _mm256_loadu_si256((__m256i *)(data + i)),\n",
    "                                    _mm256_set1_epi32(target));\n",
    "        int movemask = _mm256_movemask_epi8(cmp_vec);\n",
    "        count += _mm_popcnt_u32(movemask);\n",
    "  }\n",
    "  // we need this division to handle the fact we pulled out the bytes not the ints, but we can pull it out of the loop\n",
    "  count /=4;\n",
    "  for (; i < n; i++) {\n",
    "    if (data[i] == target) {\n",
    "      count += 1;\n",
    "    }\n",
    "  }\n",
    "  return count;\n",
    "}\n",
    "\n",
    "```\n",
    "\n",
    "This code and the assembly can be seen [https://godbolt.org/z/zdaac8fsT](https://godbolt.org/z/zdaac8fsT)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Let's take a look at the inner loop\n",
    "```\n",
    "        vpcmpeqd        ymm1, ymm0, ymmword ptr [rdi + 4*rcx]\n",
    "        vpmovmskb       r9d, ymm1\n",
    "        popcnt  r9d, r9d\n",
    "        add     rax, r9\n",
    "        add     rcx, 8\n",
    "        cmp     rcx, r8\n",
    "        jl      .LBB0_3\n",
    "```\n",
    "\n",
    "How long does this take to run?  Unlike most normal instructions, many vector operations take multiple cycles to run, and this cost is different on different cpus.\n",
    "\n",
    "For example:\n",
    "\n",
    "Vpcmpeqd \n",
    "\n",
    "| Architecture | latency | throughout |\n",
    "|--- |--- |--- |\n",
    "| all | 1 | .5 |\n",
    "\n",
    "Vpmovmskb\n",
    "| Architecture | latency | throughout |\n",
    "|--- |--- |--- |\n",
    "|Alderlake |4| 1|\n",
    "|Icelake Xeon |3| 1|\n",
    "|Sapphire Rapids |4| 1|\n",
    "|Skylake |2| 1|\n",
    "\n",
    "popcnt\n",
    "| Architecture | latency | throughout |\n",
    "|--- |--- |--- |\n",
    "| all | 3 | 1 |\n",
    "\n",
    "So this part takes 6 to 8 cycles depending on the architecture, not counting the loop overheads\n",
    "\n",
    "Can we do better?  One thing to notice is that it is expensive to move data between vector registers and normal registers, so it is beneficial to limit how often you do this.\n",
    "\n",
    "For example, the comparisons already give us -1 in each position if its equal and 0 if not, so we don't we just subtract these numbers to count up how many there are\n",
    "\n",
    "```c\n",
    "long count_ints(int* data, long n, int target) {\n",
    "  long count = 0;\n",
    "  long clean_end = (n / 8) * 8;\n",
    "  long i = 0;\n",
    "\n",
    "  auto compare = _mm256_set1_epi32(target);\n",
    "  // the counts is now a vector\n",
    "  auto counts = _mm256_setzero_si256();\n",
    "  for (; i < clean_end; i += 8) {\n",
    "      // we subtract the new comparison each time\n",
    "      // this subtracts -1 (adds 1) if its equal, else subtracts 0\n",
    "      counts = _mm256_sub_epi32(\n",
    "          counts, _mm256_cmpeq_epi32(_mm256_loadu_si256((__m256i *)(data + i)),\n",
    "                                    compare));\n",
    "  }\n",
    "  // afterword we need to pull out the different fields, but this is only at the end\n",
    "    count += _mm256_extract_epi32(counts, 0);\n",
    "    count += _mm256_extract_epi32(counts, 1);\n",
    "    count += _mm256_extract_epi32(counts, 2);\n",
    "    count += _mm256_extract_epi32(counts, 3);\n",
    "    count += _mm256_extract_epi32(counts, 4);\n",
    "    count += _mm256_extract_epi32(counts, 5);\n",
    "    count += _mm256_extract_epi32(counts, 6);\n",
    "    count += _mm256_extract_epi32(counts, 7);\n",
    "\n",
    "  for (; i < n; i++) {\n",
    "    if (data[i] == target) {\n",
    "      count += 1;\n",
    "    }\n",
    "  }\n",
    "  return count;\n",
    "}\n",
    "```\n",
    "\n",
    "Now out inner loop looks like \n",
    "```\n",
    "        vpcmpeqd        ymm3, ymm2, ymmword ptr [rdi + 4*rcx]\n",
    "        vpsubd  ymm1, ymm1, ymm3\n",
    "        add     rcx, 8\n",
    "        cmp     rcx, rax\n",
    "        jl      .LBB0_3\n",
    "```\n",
    "\n",
    "Vpcmpeqd \n",
    "\n",
    "| Architecture | latency | throughout |\n",
    "|--- |--- |--- |\n",
    "| all | 1 | .5 |\n",
    "\n",
    "Vpsubd \n",
    "\n",
    "| Architecture | latency | throughout |\n",
    "|--- |--- |--- |\n",
    "| all | 1 | .33 |\n",
    "\n",
    "This gives a total of just 2, plus the loop overhead.\n",
    "\n",
    "Now there is a bug in this code.\n",
    "\n",
    "What is it?"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Try it yourself [http://preview.speedcode.org/ide/index.html?test_1_vector_count](http://preview.speedcode.org/ide/index.html?test_1_vector_count)\n",
    "\n",
    "This is the same problem, except that you are going to count the bytes, instead of the ints.\n",
    "\n",
    "The two different boxes have different optimizations applied to them in `CountNoOpt` the compiler will not perform any loop unrolling or vectorization in `CountOpt` it will.\n",
    "\n",
    "Start with `CountNoOpt` and when you are able to make it faster than the reference move on the `CountOpt` and see if you can beat `opt serial`, then parallelize it to make it even faster\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "base",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.1.0"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
